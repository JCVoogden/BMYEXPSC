********************************************************************
*               CALCUL DE LA CLE RIB                               *
*                                                                  *
********************************************************************
RECEVOIR PCODE_BANQUE PCODE_GUICHET PCODE_COMPTE PCLE_RIB PCODE_ERREUR
*
PLACER_TABLE 'ABCDEFGHIJKLMNOPQRSTUVWXYZ' C1
PLACER_TABLE '12345678912345678923456789' C2
*
PCODE_ERREUR = *BLANK
*
WN10 = PCODE_BANQUE * 100000
WN10 = WN10 + PCODE_GUICHET
WN10 = WN10 / 97
RESTE WN20
WA2 = WN20
PLACER_TABLE WA2 NCP(1)
PLACER_TABLE PCODE_COMPTE CPT
TNC = *BLANK
I = 11
J = 11
TANT_QUE I <> 0
SI CPT(I) <> *BLANK
PLACER_A_DROITE CPT(I) TNC(J)
J = J - 1
FIN
I = I - 1
REFAIRE
*
PLACER_TABLE TNC CPT
*
I = 1
TANT_QUE I <= 11
J = 1
SI CPT(I) < '0'
SI CPT(I) = ' '
CPT(I) = '0'
SINON
RECHERCHE = CPT(I) C1(J)
SI *TROUVE = '0'
PCODE_ERREUR = '2'
RETOURNER
FIN
CPT(I) = C2(J)
FIN
FIN
I = I + 1
REFAIRE
*
PLACER_TABLE CPT NCP(3)
NCP(14) = '0'
NCP(15) = '0'
PLACER_TABLE NCP WNZ15
WNN15 = WNZ15
WNN15 = WNN15 / 97
RESTE WCODE_RIB
WCODE_RIB = 97 - WCODE_RIB
SI WCODE_RIB <> PCLE_RIB
PCODE_ERREUR = '1'
PCLE_RIB = WCODE_RIB
FIN
*
RETOURNER
