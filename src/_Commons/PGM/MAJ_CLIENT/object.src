* Niveau I -
*------------------
INITIALISER 01
TRAITER 01
*-------------------------
TRANSACTION 01
SI *F03 OU *F12
  TERMINER
FIN
VERIFIER 01
INITIALISER 02
TRAITER 02
*-------------------------
TRANSACTION 02
SI *F12
  TRAITER 01
FIN
VERIFIER 02
SI *F06
  VALIDER 02
  INITIALISER 01
  TRAITER 01
FIN
TRAITER 02
******************
INITIALISATION  01
******************
DEBUT_STD
XNOM_PGM        = *NOM_PGM
ZZCOD_CLIENT    = *BLANK
FIN_STD
******************
TRT_GUIDE       01
******************
DEBUT_STD
* Guide d'affichage de la fenêtre des CLIENTS
GUIDE ZZCOD_CLIENT
APPELER FEN_CLIENT P_COD_RETOUR P_COD_ACTION ZZCOD_CLIENT
FIN_GUIDE
FIN_STD
FIN_TRT_GUIDE
******************
VERIFICATION    01
******************
DEBUT_STD
REGLE_GESTION 0001 ZZCOD_CLIENT
SI ZZCOD_CLIENT = *BLANK
  ANOMALIE
FIN
FIN_STD
******************
INITIALISATION  02
******************
DEBUT_STD
LIRE            CLIENTS_DA
PRESENTER       CLIENTS_DA
LIRE            PERSONN_DB
PRESENTER       PERSONN_DB
LIRE            REGLEMT_DC
PRESENTER       REGLEMT_DC
XNOM_PGM        = *NOM_PGM
FIN_STD
******************
TRT_GUIDE       02
******************
DEBUT_STD
* Guide d'affichage de la fenêtre des REGLEMENTS
GUIDE ZZCOD_REGLEMENT
APPELER FEN_REGLEMENT P_COD_RETOUR P_COD_ACTION ZZCOD_REGLEMENT
FIN_GUIDE
FIN_STD
FIN_TRT_GUIDE
******************
VERIFICATION    02
******************
DEBUT_STD
REGLE_GESTION 0001 ZZNOM_CLIENT
SI ZZNOM_CLIENT = *BLANK
  ANOMALIE
FIN
REGLE_GESTION 0001 ZZADR_01_CLI
SI ZZADR_01_CLI = *BLANK
  ANOMALIE
FIN
REGLE_GESTION 0001 ZZBUR_DIS_CLI
SI ZZBUR_DIS_CLI = *BLANK
  ANOMALIE
FIN
REGLE_GESTION 0001 ZZCOD_MAT_PERS
SI ZZCOD_MAT_PERS = *BLANK
  ANOMALIE
FIN
* Règle de gestion de type contrôle
* Contrôle du CODE PERSONNE
SI ZZCOD_MAT_PERS <> *BLANK
  REGLE_GESTION 0018 ZZCOD_MAT_PERS
  WG_COD_PER = ZZCOD_MAT_PERS
  LIRE            RPERSONNL_COD
  SI              RPERSONNL_COD  N_EXISTE_PAS
    ZZNOM_PERS = *BLANK
    INIT_MSG ZZCOD_MAT_PERS
    ANOMALIE
  SINON
    ZZNOM_PERS = PNOM_PERS
  FIN
FIN
REGLE_GESTION 0001 ZZCOD_REGLEMENT
SI ZZCOD_REGLEMENT = *BLANK
  ANOMALIE
FIN
* Règle de gestion de type contrôle
* Contrôle du CODE REGLEMENT
REGLE_GESTION 0004 ZZCOD_REGLEMENT
WG_COD_RGT = ZZCOD_REGLEMENT
LIRE            RREGLEMT_COD
SI              RREGLEMT_COD  N_EXISTE_PAS
  ZZLIB_REGLEMT = *BLANK
  INIT_MSG ZZCOD_REGLEMENT
  ANOMALIE
SINON
  ZZLIB_REGLEMT = RLIB_REGLEMT
FIN
FIN_STD
******************
VALIDATION      02
******************
DEBUT_STD
RANGER          CLIENTS_DA
FIN_STD
