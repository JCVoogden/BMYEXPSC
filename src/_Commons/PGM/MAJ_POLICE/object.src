*
* Remplacez VUE_???? Par le nom de la vue gérer
* Niveau I -
*------------------
INITIALISER 01
TRAITER 01
*-------------------------
TRANSACTION 01
SI *F03 OU *F12
  TERMINER
FIN
VERIFIER 01
INITIALISER 02
TRAITER 02
*-------------------------
TRANSACTION 02
SI *F03
  TERMINER
FIN
SI *F12
  TRAITER 01
FIN
* Par F13 appel de la procédure de suppression de l'nregistrement
* Afficher
SI *F13
  TRAITER_PROC SUPPRE
  TRAITER 01
FIN
VERIFIER 02
VALIDER 02
INITIALISER 01
TRAITER 01
*
DEBUT_PROCEDURE SUPPRE
*-------------------------  Procédure de suppression
SUPPRIMER   POLICE_DB
FIN_PROCEDURE
******************
INITIALISATION  01
******************
DEBUT_STD
XNOM_PGM        = *NOM_PGM
ZZCOD_CLIENT    = *BLANK
ZZNUM_POLICE    = *BLANK
FIN_STD
******************
TRT_GUIDE       01
******************
DEBUT_STD
* Guide d'affichage de la fenêtre des CLIENTS
GUIDE ZZCOD_CLIENT
APPELER FEN_CLIENT P_COD_RETOUR P_COD_ACTION ZZCOD_CLIENT
FIN_GUIDE
* Guide d'affichage de la fenêtre des POLICE par CLIENT
GUIDE ZZNUM_POLICE
APPELER FEN_POLICE P_COD_RETOUR P_COD_ACTION ZZNUM_POLICE ZZCOD_CLIENT
FIN_GUIDE
FIN_STD
FIN_TRT_GUIDE
******************
VERIFICATION    01
******************
DEBUT_STD
REGLE_GESTION 0001 ZZCOD_CLIENT
SI ZZCOD_CLIENT = *BLANK
ANOMALIE
FIN
* Règle de gestion de type contrôle
* Contrôle du CODE CLIENT
REGLE_GESTION 0001 ZZCOD_CLIENT
WG_COD_CLI = ZZCOD_CLIENT
LIRE            RCLIENTS_COD
SI              RCLIENTS_COD  N_EXISTE_PAS
  ZZNOM_CLIENT  = *BLANK
  ZZBUR_DIS_CLI = *BLANK
  ZZNUM_TEL_CLI = *BLANK
  INIT_MSG ZZCOD_CLIENT
  ANOMALIE
SINON
  ZZNOM_CLIENT  = CNOM_CLIENT
  ZZBUR_DIS_CLI = CBUR_DIS_CLI
  ZZNUM_TEL_CLI = CNUM_TEL_CLI
FIN
REGLE_GESTION 0001 ZZNUM_POLICE
SI ZZNUM_POLICE = *BLANK
ANOMALIE
FIN
FIN_STD
******************
INITIALISATION  02
******************
DEBUT_STD
LIRE            CLIENTS_DA
PRESENTER       CLIENTS_DA
LIRE            POLICE_DB
PRESENTER       POLICE_DB
LIRE            VEHICULE_DC
PRESENTER       VEHICULE_DC
XNOM_PGM        = *NOM_PGM
FIN_STD
******************
TRT_GUIDE       02
******************
DEBUT_STD
* Guide d'affichage de la fenêtre des VEHICULES
GUIDE ZZNUM_IMA_VEHIC
APPELER FEN_VEHICULE P_COD_RETOUR P_COD_ACTION ZZNUM_IMA_VEHIC
FIN_GUIDE
FIN_STD
FIN_TRT_GUIDE
******************
VERIFICATION    02
******************
DEBUT_STD
REGLE_GESTION 0001 ZZNUM_IMA_VEHIC
SI ZZNUM_IMA_VEHIC = *BLANK
ANOMALIE
FIN
* Règle de gestion de type contrôle
* Contrôle du CODE VEHICULE
REGLE_GESTION 0012 ZZNUM_IMA_VEHIC
WG_COD_VEH = ZZNUM_IMA_VEHIC
LIRE            RVEHICUL_COD
SI              RVEHICUL_COD  N_EXISTE_PAS
 ZZMAR_VEHICULE = *BLANK
 ZZMDL_VEHICULE = *BLANK
 ZZPUI_VEHICULE = *BLANK
  ANOMALIE
SINON
 ZZMAR_VEHICULE = VMAR_VEHICULE
 ZZMDL_VEHICULE = VMDL_VEHICULE
 ZZPUI_VEHICULE = VPUI_VEHICULE
FIN
FIN_STD
******************
VALIDATION      02
******************
DEBUT_STD
RANGER          POLICE_DB
FIN_STD
