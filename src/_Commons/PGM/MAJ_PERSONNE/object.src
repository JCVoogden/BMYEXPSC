* Mise à jour avec une seule transaction avec réception de paramètre
*
* La variable P_COD_ACTION sert à déterminer si la sorti s'est
* faites par F03 (EXit) ou par F12 (PRécédent)
*
* La variable P_COD_RETOUR sert à Ppositionner un eventuel problème
* dans le programme à ramener au programme appelant
*
* Recherchez P_???? et remplacer la zone clé du fichier à traiter
*
* Recherchez Z_???? et remplacer la zone écran clé du fichier
*
*
RECEVOIR P_COD_RETOUR P_COD_ACTION PWCOD_MAT_PERS
INITIALISER 01
TRAITER 01
*-------------------------
TRANSACTION 01
SI *F03
  P_COD_ACTION = 'EX'
  P_COD_RETOUR = *BLANK
  TERMINER
FIN
SI *F04
  ENVOYER_MSG SYS9900
  TRAITER 01
FIN
SI *F12
  P_COD_ACTION = 'PR'
  P_COD_RETOUR = *BLANK
  TERMINER
FIN
VERIFIER 01
VALIDER 01
P_COD_RETOUR = *BLANK
TERMINER
******************
INITIALISATION  01
******************
ZZCOD_MAT_PERS = PWCOD_MAT_PERS
LIRE            PERSONN_DA
PRESENTER       PERSONN_DA
XNOM_PGM        = *NOM_PGM
WG_COD_PER = PCOD_MAT_PER2
LIRE           RPERSONNL_COD
SI             RPERSONNL_COD  EXISTE
  ZNOM_HIERARCHIE = PNOM_PERS
SINON
  ZNOM_HIERARCHIE = *BLANK
FIN
SI P_COD_ACTION = 'MO'
  PROTEGER ZZCOD_MAT_PERS
FIN
*****************
VERIFICATION 01
*****************
* Si  création le matricule saisie ne doit pas exister
REGLE_GESTION MSG0019 ZZCOD_MAT_PERS
SI PWCOD_MAT_PERS = *BLANK
  LIRE            PERSONN_DA
  SI PERSONN_DA EXISTE
    LIBERER         PERSONN_DA
    INIT_MSG ZZCOD_MAT_PERS
    ERREUR
  FIN
FIN
DEBUT_STD
REGLE_GESTION MSG0001 ZZCOD_MAT_PERS
SI ZZCOD_MAT_PERS = *BLANK
  ANOMALIE
FIN
REGLE_GESTION MSG0001 ZZNOM_PERS
SI ZZNOM_PERS = *BLANK
  ANOMALIE
FIN
REGLE_GESTION 0001 ZZTAU_CON_PERS
SI ZZTAU_CON_PERS = 0
  ANOMALIE
FIN
FIN_STD
* Contrôle du CODE PERSONNE
SI ZZCOD_MAT_PER2 <> *BLANK
  REGLE_GESTION 0018 ZZCOD_MAT_PER2
  WG_COD_PER = ZZCOD_MAT_PER2
  LIRE            RPERSONNL_COD
  SI              RPERSONNL_COD  N_EXISTE_PAS
    ZNOM_HIERARCHIE = *BLANK
    INIT_MSG ZZCOD_MAT_PER2
    ANOMALIE
  SINON
    ZNOM_HIERARCHIE = PNOM_PERS
  FIN
FIN
*************
VALIDATION 01
*************
DEBUT_STD
RANGER          PERSONN_DA
FIN_STD
*****************
TRT_GUIDE 01
*****************
DEBUT_STD
* Guide d'affichage de la fenêtre des PERSONNES
GUIDE ZZCOD_MAT_PERS
APPELER FEN_PERSONNE P_COD_RETOUR P_COD_ACTION ZZCOD_MAT_PERS
FIN_GUIDE
* Guide d'affichage de la fenêtre des PERSONNES
GUIDE ZZCOD_MAT_PER2
APPELER FEN_PERSONNE P_COD_RETOUR P_COD_ACTION ZZCOD_MAT_PER2
WG_COD_PER = ZZCOD_MAT_PER2
LIRE            RPERSONNL_COD
SI              RPERSONNL_COD  N_EXISTE_PAS
  ZNOM_HIERARCHIE = *BLANK
SINON
  ZNOM_HIERARCHIE = PNOM_PERS
FIN
FIN_GUIDE
FIN_STD
SI *F04
  ENVOYER_MSG SYS9900
FIN
FIN_TRT_GUIDE
